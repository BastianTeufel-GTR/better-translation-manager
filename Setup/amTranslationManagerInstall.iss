; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Better Translation Manager"
#define MyAppFolderName "Melander\Better Translation Manager"
#define MyAppExeName "amTranslationManager.exe"
;#define MyAppSourceFolder "..\Bin\Win32\Debug\"
#define MyAppSourceFolder "..\Bin\Win32\Release\"
#define MyAppVersion GetFileVersion(MyAppSourceFolder + '\' + MyAppExeName)
#define MyAppPublisher "Anders Melander"
#define MyAppURL "https://bitbucket.org/anders_melander/better-translation-manager"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{9208123E-2CDD-4708-9751-FC94AA95ACA2}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppFolderName}
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
OutputBaseFilename=amTranslationManagerInstall-{#MyAppVersion}
SetupIconFile=..\Source\resources\amTranslationManager.ico
UninstallDisplayIcon={app}\amTranslationManager.exe
MinVersion=0,6.1
WizardSmallImageFile=.\Resources\logo 55x55.bmp
WizardImageStretch=False
SolidCompression=True
VersionInfoVersion={#MyAppVersion}
AlwaysShowDirOnReadyPage=True
Uninstallable=not IsPortable
PrivilegesRequired=none
UsePreviousPrivileges=False

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked; Check: not IsPortable

[Files]
Source: "{#MyAppSourceFolder}\amTranslationManager.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#MyAppSourceFolder}\amResourceModuleBuilder.exe"; DestDir: "{app}"; Flags: ignoreversion; Components: buildtool
Source: "Files\portable"; DestDir: "{app}"; Check: IsPortable
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
Source: ".\Files\Dictionaries\*.*"; DestDir: "{app}\Dictionaries"; Components: spellcheck
Source: ".\Files\Translation Memory\TranslationMemory.dat"; DestDir: "{userappdata}\TranslationManager\"; Flags: confirmoverwrite; Components: tm; Check: not IsPortable
Source: ".\Files\Examples\*.*"; DestDir: "{userdocs}\Translation Examples\"; Flags: recursesubdirs createallsubdirs; Components: examples; Check: not IsPortable

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Check: not IsPortable
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon; Check: not IsPortable
Name: "{group}\Translation Examples"; Filename: "{userdocs}\Translation Examples"; Components: examples; Check: not IsPortable

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[Types]
Name: "full"; Description: "Full installation"
;Name: "compact"; Description: "Compact installation"
Name: "custom"; Description: "Custom installation"; Flags: iscustom
Name: "portable"; Description: "Portable installation"

[ThirdParty]
UseRelativePaths=True

[Components]
Name: "examples"; Description: "Examples"; Types: full custom; Check: not IsPortable
Name: "spellcheck"; Description: "Spell Check dictionaries"; Types: full custom portable
Name: "tm"; Description: "Sample Translation Memory"; Types: full custom portable
Name: "buildtool"; Description: "Command line tool"; Types: portable

[Registry]
Root: "HKCU"; Subkey: "Software\Melander\TranslationManager"; ValueType: dword; ValueName: "Installed"; ValueData: "1"; Flags: createvalueifdoesntexist uninsdeletevalue
Root: "HKCU"; Subkey: "Software\Melander\TranslationManager"; ValueType: string; ValueName: "InstalledVersion"; ValueData: "{#MyAppVersion}"; Flags: createvalueifdoesntexist
Root: "HKCU"; Subkey: "Software\Melander\TranslationManager"; ValueType: string; ValueName: "InstallRoot"; ValueData: "{app}"; Flags: createvalueifdoesntexist
Root: "HKCU"; Subkey: "Software\Melander\TranslationManager\System"; ValueType: dword; ValueName: "Portable"; ValueData: "1"; Check: IsPortable

[Dirs]
Name: "{userappdata}\TranslationManager\Dictionaries\Custom spell check dictionaries"; Check: not IsPortable

[Code]
var
  FIsPortable: boolean;

function IsPortable(): boolean;
begin
  Result := FIsPortable;
end;

function NextButtonClick(CurPageID: Integer): Boolean;
begin
  if (CurPageID = wpSelectComponents) then
    FIsPortable := (WizardSetupType(False) = 'portable');

  Result := True;
end;

function ShouldSkipPage(PageID: Integer): Boolean;
begin
  Result := False;

  if (PageID = wpSelectProgramGroup) then
    Result := IsPortable;
end;
